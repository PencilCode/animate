---
title: Keys and Objects
mission: Find the key and use it to open the door.
layout: code
codeLevel: 2
codeOrder: 3
thumb: 'dot'
tags: ['imagine','index']
refs: ['do', 'function', 'menu', 'variables']
code: |
  # This is a variable! You can name variables whatever you like.
  found_the_key = false
  under_the_hidden_tree = ->
    do cs
    write """
      Finally, after a long walk in the forest, you 
      find yourself under an old, mossy oak tree.
      The sunlight glimmers against something
      hidden in its gnarled roots...
    """
    menu
      "Look closer at the roots": tree_roots
      "Return to the castle": castle

  tree_roots = ->
    do cs
    write "You dig among the roots with your fingers and find an old key."
    write "It's dirty and rusty, but it looks like it would still work."
    write """
      <img src="/images/key.png">
      """
    # Here's where we make sure to remember you found the key!
    found_the_key = true
    menu
      "Go back to the castle": castle

  castle = ->
    do cs
    write """
      You walk to the castle and go inside
      to look at the treasury door.
    """
    # If you found the key, then you get to unlock the door.
    if found_the_key
      menu
        "Unlock the door": treasure_room
    # If you haven't found it yet...
    if not found_the_key
      write "If only you had the key, you might be able to save the kingdom..."
      menu "Search for the key": under_the_hidden_tree

  treasure_room = ->
    write """
      The key fits! You open the door, and among 
      the treasure you see a Crown...
    """

  # Start the game
  do castle
---
