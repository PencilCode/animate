---
title: First Dot
mission: Pick a <a href="/ref/colors.html">color</a> and make a dot.
layout: code
codeLevel: 0
codeOrder: 1
thumb: 'dot'
tags: ['draw','index']
refs: ['colors', 'dot']
code: |
  dot purple, 100
---
dotcount = 0
colors = {}
sizes = []
originaldot = window.dot
window.dot = ->
  dotcount += 1
  color = black
  size = 8
  for a in arguments
    if isNaN(a) then color = a
    else size = a
  colors[color] = true
  sizes.push(size)
  originaldot.apply(null, arguments)
hints = ->
  if dotcount == 0
    return
  if dotcount == 1 and sizes[0] == 100 and colors.purple
    popup """
      <p>Can you change the color?
      <p>HINT: find the colors reference page.
    """
  else if dotcount == 1 and sizes[0] == 100
    popup """
      <p>Can you make a different size?
      <p>HINT: the number is the diameter.
    """
  else if dotcount == 1
    popup """
      <p>Try adding a second dot!<br>
      <p>HINT: add a second line of code.
    """
  else if _.keys(colors).length == 1
    popup """
      <p>Are your dots all the same color?
      <p>HINT: try picking different colors.
    """
  else if _.uniq(sizes).length == 1
    popup """
      <p>Are your dots all the same size?
      <p>HINT: try picking a different size for a dot.
    """
  else if _.min(sizes) <= 16
    popup """
      <p>HINT: your dot is smaller than the turtle
    """
  else if not _.every(sizes, (v, i) -> i is 0 or sizes[i - 1] >= v)
    popup """
      <p>Did you cover up a dot?<br>
      Try drawing the bigger one first.
    """
  else if dotcount < 4
    popup """
      <p>Challenge: try drawing a bullseye<br>
    """
  else
    popup """
      <p>Beautiful!
    """
click ->
  if dotcount is 0
    popup """
      <p>Click on the blue play button to run the program.
    """

setTimeout((-> done -> setTimeout hints, 500), 500)
